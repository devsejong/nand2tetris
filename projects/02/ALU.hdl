// This file is part of www.And2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:

    //////////// 전처리 ///////////////

    // 만약 zx = true 라면 0을 반환함
    Mux16(a=x, b[0..15]=false, sel=zx, out=x0);
    
    // 만약 nx = true 라면 Not의 결과 반환
    Not16(in=x0, out=x1);
    Mux16(a=x0, b=x1, sel=nx, out=inputX);


    // 만약 zy = true 라면 0을 반환함
    Mux16(a=y, b[0..15]=false, sel=zy, out=y0);
    
    // 만약 ny = true 라면 Not의 결과 반환
    Not16(in=y0, out=y1);
    Mux16(a=y0, b=y1, sel=ny, out=inputY);

    //////////// 계산 ///////////////

    Add16(a=inputX, b=inputY, out=resAdd);
    And16(a=inputX, b=inputY, out=resAnd);
    Mux16(a=resAnd, b=resAdd, sel=f, out=resF);

    //////////// 후처리 ///////////////

    // 반전
    Not16(in=resF, out=resNf);
    Mux16(a=resF, b=resNf, sel=no, out[0..7]=res0, out[8..15]=res1, out[15]=ng, out=out);

    //////////// 모든값이 0인지 여부 확인 //////////
    
    Or8Way(in=res0, out=allFalse0);
    Or8Way(in=res1, out=allFalse1);
    Or(a=allFalse0, b=allFalse1, out=nzr);
    Not(in=nzr, out=zr);
}